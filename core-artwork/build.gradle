/*
 * Copyright (c) 2015 OpenSilk Productions LLC
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.hugo'

// Versioning
def versionMajor = 0
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

assert(versionMinor < 100) // Double digits
assert(versionPatch < 100) // Double digits
assert(versionBuild < 100) // Double digits

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName String.format("%d.%d.%d-SNAPSHOT", versionMajor, versionMinor, versionPatch)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

//    packagingOptions {
//        exclude 'META-INF/services/javax.annotation.processing.Processor'
//    }

    if (file('../../KeyStore').exists()) {
        def Properties otherProps = new Properties();
        otherProps.load(new FileInputStream(file('../../KeyStore/orpheus.properties')))
        defaultConfig.buildConfigField "String", "LASTFM_KEY", otherProps.lastfmKey
    } else {
        println "No KeyStore directory found! (You can ignore this warning if your name is not drew)"
        defaultConfig.buildConfigField "String", "LASTFM_KEY", "null"
    }

    defaultPublishConfig "debug"

}

dependencies {
    provided(group: 'com.google.dagger', name:'dagger-compiler', version: rootProject.ext.dagger2Version)
    compile (group: 'com.android.support', name: 'palette-v7', version: rootProject.ext.supportLibVersion)
    compile (group: 'com.google.code.gson', name: 'gson', version: rootProject.ext.gsonVersion)
    compile (group: 'commons-io', name: 'commons-io', version: rootProject.ext.commonsIoVersion)
    compile project(':common-core')
    compile project(':common-ui')
    compile project(':core-model')
    compile project(':core-lastfm')
    compile project(':core-volley')
    compile 'com.jakewharton:disklrucache:2.0.2'

    androidTestCompile('com.squareup:fest-android:1.0.0')

    testCompile (group:"junit", name:"junit", version: rootProject.ext.testJunitVersion)
    testCompile (group:"org.assertj", name:"assertj-core", version: rootProject.ext.testAssertJVersion)
    testCompile (group:"org.robolectric", name:"robolectric", version : rootProject.ext.testRobolectricVersion)
    testCompile (group:"org.mockito", name:"mockito-core", version: rootProject.ext.testMockitoVersion)
    testCompile "commons-io:commons-io:2.4"
    testCompile 'com.squareup.okhttp:mockwebserver:2.5.0'
}
