/*
 * Copyright (c) 2014 OpenSilk Productions LLC
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'org.robolectric'

// Versioning
def versionMajor = 0
def versionMinor = 2
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

assert(versionMinor < 100) // Double digits
assert(versionPatch < 100) // Double digits
assert(versionBuild < 100) // Double digits

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "org.opensilk.music.plugin.drive"
        minSdkVersion 15
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName String.format("%d.%d.%d", versionMajor, versionMinor, versionPatch)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix rootProject.getDebugVersionSuffix()
        }
    }

    if (file('../../KeyStore').exists()) {
        signingConfigs {
            release {
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(file('../../KeyStore/release2.properties')))
                storeFile file(keyProps["store"])
                keyAlias keyProps["alias"]
                storePassword keyProps["storePass"]
                keyPassword keyProps["pass"]
            }
        }
        buildTypes.release.signingConfig signingConfigs.release
        signingConfigs {
            debug {
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(file('../../KeyStore/debug2.properties')))
                storeFile file(keyProps["store"])
                keyAlias keyProps["alias"]
                storePassword keyProps["storePass"]
                keyPassword keyProps["pass"]
            }
        }
        buildTypes.debug.signingConfig signingConfigs.debug
    } else {
        println "No KeyStore directory found! (You can ignore this warning if your name is not drew)"
    }

}

robolectric {
    include '**/*Test.class'
    //jvmArgs '-XX:MaxPermSize=512m', '-XX:-UseSplitVerifier'
    afterTest {
        descriptor, result -> println "Executing test for ${descriptor.name} with result: ${result.resultType}"
    }
}

dependencies {
    provided(group: 'com.google.dagger', name:'dagger-compiler', version: rootProject.ext.dagger2Version)

    compile project(':plugin-common')
    compile project(':core-library')

    compile (group:'com.jakewharton', name:'butterknife', version: rootProject.ext.butterKnifeVersion)

    //https://github.com/google/google-api-java-client/releases
    compile ('com.google.api-client:google-api-client-android:1.20.0') {
        exclude module: 'httpclient'
        exclude group: 'com.google.guava'
    }
//    compile ('com.google.api-client:google-api-client-gson:1.20.0') {
//        exclude module: 'httpclient'
//    }
    //https://developers.google.com/api-client-library/java/apis/drive/v2
    compile ('com.google.apis:google-api-services-drive:v2-rev168-1.20.0') {
        exclude module: 'httpclient'
        exclude group: 'com.google.guava'
    }
    compile 'com.google.android.gms:play-services-identity:6.5.87'
    //workaround for build fail similar to https://github.com/groovy/groovy-android-gradle-plugin/issues/32
    //default is 13 which borks dagger
    compile 'com.google.guava:guava-jdk5:17.0'

    testCompile('junit:junit:4.12')
    testCompile('org.robolectric:robolectric:2.4') {
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
    }
    testCompile('com.squareup:fest-android:1.0.0')
    testCompile('org.mockito:mockito-core:1.10.0')
}
